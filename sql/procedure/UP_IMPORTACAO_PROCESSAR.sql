IF OBJECT_ID('imp.UP_IMPORTACAO_PROCESSAR', 'P') IS NOT NULL
	DROP PROCEDURE imp.UP_IMPORTACAO_PROCESSAR;
GO

CREATE PROCEDURE imp.UP_IMPORTACAO_PROCESSAR (
	@ID INT
)
AS
SET NOCOUNT ON;

SELECT	R.*, I.SEGURADORA_ID
INTO	#REGISTRO
FROM	imp.REGISTRO R
INNER	JOIN imp.IMPORTACAO I
		ON I.ID = R.IMPORTACAO_ID
		AND I.ID = @ID
;

/*SEGURADO*/
DECLARE	@SEGURADO TABLE (ID INT, CPF VARCHAR(14), REGISTRO_ID INT);

INSERT	INTO SEGURADO (
	NOME, CPF, BANCO, AGENCIA, DIGITO_AGENCIA, CONTA, DIGITO_CONTA
	, BENEFICIARIO, CPF_BENEFICIARIO, EMAIL, CONTA_CADASTRADA
)
OUTPUT inserted.ID, inserted.CPF, null INTO @SEGURADO
SELECT	DISTINCT R.NOME_SEGURADO, dbo.UF_TAMANHO_CPF_CNPJ(R.DOCUMENTO) AS CPF, ISNULL(R.NOME_BANCO, '') AS BANCO, ISNULL(R.NUMERO_AGENCIA, '') AS AGEN
		, ISNULL(R.DIGITO_AGENCIA, '') AS NumAge, ISNULL(R.NUMERO_CONTA, '') Conta, ISNULL(R.DIGITO_CONTA, '') NumConta, ISNULL(R.NOME_SEGURADO, '') SeguradoBenef
		, dbo.UF_TAMANHO_CPF_CNPJ(R.DOCUMENTO) cpfBenef, ISNULL(R.EMAIL_CONTATO, '') Email, 0 ContaCadastrada
FROM	#REGISTRO R
;

UPDATE S SET S.REGISTRO_ID = R.ID
FROM	@SEGURADO S
INNER	JOIN #REGISTRO R
		ON S.CPF = dbo.UF_TAMANHO_CPF_CNPJ(R.DOCUMENTO)
;

/*ND*/
DECLARE @ND TABLE (ID INT, NOME VARCHAR(20), REGISTRO_ID INT);

INSERT	INTO NOTA_DEBITO (NOME, DATA_EMISSAO, DATA_ENVIO, OBSERVACAO) 
OUTPUT	inserted.ID, inserted.NOME, null INTO @ND
SELECT	DISTINCT LTRIM(RTRIM(R.NOTA_DEBITO)), R.DATA_EMISSAO_ND, R.DATA_ENVIO, R.COBERTURA_RECLAMADA
FROM	#REGISTRO R
;

UPDATE	ND
SET		REGISTRO_ID = R.ID
FROM	@ND ND
INNER	JOIN #REGISTRO R
		ON ND.NOME = LTRIM(RTRIM(R.NOTA_DEBITO))
;

/*PATROCINIO*/
DECLARE @PATROCINIO TABLE (ID INT, REGISTRO_ID INT);

INSERT	INTO @PATROCINIO (ID, REGISTRO_ID)
SELECT	P.ID, R.ID
FROM	#REGISTRO R
INNER	JOIN PATROCINIO P
		ON P.SEGURADORA_ID = R.SEGURADORA_ID
INNER	JOIN REPRESENTANTE RE
		ON RE.ID = P.REPRESENTANTE_ID
		AND RE.NOME = R.CIA
;

/*SINISTRO*/
INSERT	INTO SINISTRO (
	NUMERO_SINISTRO, PROCESSO, BILHETE, REFERENCIA, DATA_ATENDIMENTO, DATA_OCORRENCIA
	, VOUCHER, COBERTURA, CUSTO_ORIGINAL, TIPO_MOEDA, VALOR_FINAL, FEE, TIPO_MOEDA_FEE
	, VALOR_DOLAR, VALOR_REAL, DOLAR, REGISTRO_ID, NOTA_DEBITO_ID, SEGURADO_ID, PATROCINIO_ID
)
SELECT	R.NUMERO_SINISTRO, R.PROCESSO, R.BILHETE, R.REFERENCIA, R.DATA_ATENDIMENTO, R.DATA_OCORRENCIA
		, R.VOUCHER, R.COBERTURA_RECLAMADA, R.CUSTO_ORIGINAL, R.TIPO_MOEDA, R.VALOR_FINAL, R.FEE, R.TIPO_MOEDA_FEE
		, R.VALOR_ND_US, R.VALOR_ND_RS, R.TAXA_CAMBIO, R.ID, ND.ID, S.ID, P.ID
FROM	#REGISTRO R
INNER	JOIN NOTA_DEBITO ND ON ND.NOME = LTRIM(RTRIM(R.NOTA_DEBITO))
INNER	JOIN SEGURADO S ON S.CPF = dbo.UF_TAMANHO_CPF_CNPJ(R.DOCUMENTO)
INNER	JOIN @PATROCINIO P ON P.REGISTRO_ID = R.ID
;
GO